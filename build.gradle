plugins {
    id "java"
    id "com.modrinth.minotaur" version "2.+"
}

group = "org.sysapp.runkang10.clientcrasher"
version = "1.0.0"
String configVersion = "0.1.0"

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "clientcrasher-is-unique"
    versionNumber = version
    versionType = "release"
    uploadFile = jar
    gameVersions = ["1.21", "1.21.1"]
    loaders = ["paper"]
    syncBodyFrom = rootProject.file("README.md").text
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly files("/libs/ProtocolLib.jar")
    compileOnly files("/libs/IF.jar")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [
            version: version,
            configVersion: configVersion
    ]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching(["paper-plugin.yml", "config.yml", "db.yml", "*.lang.yml"]) {
        expand props
    }
}